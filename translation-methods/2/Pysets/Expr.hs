module Pysets.Expr (Expr(..), shB, expr2Dot) where

import Pysets.Tokens
import qualified Data.Sequence as Seq
import Data.Sequence (Seq, (><))
import Control.Monad.State
import Data.Foldable (toList)

data Expr
	= Not { expr :: Expr, ePos :: Position }
	| And { lexpr :: Expr, rexpr :: Expr, ePos :: Position }
	| Or { lexpr :: Expr, rexpr :: Expr, ePos :: Position }
	| Xor { lexpr :: Expr, rexpr :: Expr, ePos :: Position }
	| In { lexpr :: Expr, rexpr :: Expr, ePos :: Position }
	| Name { name :: String, ePos :: Position }
	deriving (Eq)

instance Show Expr where
	show Not {..} = "!" ++ show expr
	show e@And {} = shB "&" e
	show e@Or {} = shB "|" e
	show e@Xor {} = shB "^" e
	show e@In {} = shB "âˆˆ" e
	show Name{..} = name

data MyState = MyState { nId :: Int, ans :: Seq Char }

type ST a = State MyState a

s2s = Seq.fromList

append :: (Seq Char) -> ST ()
append a = do
	modify \s@MyState { ans } -> s { ans = ans >< a }

mkId :: ST (Seq Char)
mkId = do
	prev <- gets nId
	modify (\s -> s { nId = prev + 1 })
	return $ s2s "v" >< s2s (show prev)

shwD :: Expr -> ST (Seq Char)
shwD Name { name } = do
	i <- mkId
	append $ i >< s2s "[shape=box,label=\"" >< s2s name >< s2s "\"];"
	return i
shwD Not { expr } = do
	i <- mkId
	nxt <- shwD expr
	append $ i >< s2s "[shape=circle,label=\"not\"];"
	append $ i >< s2s "->" >< nxt >< s2s ";"
	return i
shwD e@And {} = mkBop e "and"
shwD e@Or {} = mkBop e "or"
shwD e@Xor {} = mkBop e "xor"
shwD e@In {} = mkBop e "in"

mkBop e s = do
	i <- mkId
	nl <- shwD $ lexpr e
	nr <- shwD $ rexpr e
	append $ i >< s2s "[shape=circle,label=\"" >< s2s s >< s2s "\"];"
	append $ i >< s2s "->" >< nl >< s2s ";"
	append $ i >< s2s "->" >< nr >< s2s ";"
	return i


expr2Dot :: Expr -> String
expr2Dot e = toList $ s2s "digraph T {" >< ans (execState (shwD e) (MyState 0 Seq.empty)) >< s2s "}"

shB c e = "(" ++ show (lexpr e) ++ c ++ show (rexpr e) ++ ")"

instance Pos Expr where
	position = ePos

